#
# GNU GRUB, the Grand Unified Boot Loader.
#
{
  pkgs,
  lib,
  config,
  ...
}:
with lib; let
  catppuccin = pkgs.catppuccin-grub.override {
    flavor = "mocha";
  };

  monitors = config.myHardware.monitors;
  mainMonitor =
    lists.findSingle (monitor: monitor.primary)
    (lists.head monitors)
    (lists.head monitors)
    monitors;
in {
  config.boot = {
    #
    # Enable "Silent Boot"
    #
    # All Kernel Messages with a log level smaller than this setting will be printed to the console.
    # 0 (KERN_EMERG)          system is unusable
    # 1 (KERN_ALERT)          action must be taken immediately
    # 2 (KERN_CRIT)           critical conditions
    # 3 (KERN_ERR)            error conditions
    # 4 (KERN_WARNING)        warning conditions
    consoleLogLevel = 3;
    initrd.verbose = false; # Removes the mandatory messages generated by the NixOS scripts
    kernelParams =
      [
        "quiet" # Runs kernel in non-verbose mode (i.e. no output)
        "splash" # Show fancy loading screen instead of log messages
        "udev.log_priority=3" # Hide systemd init messages
      ]
      ++ map (
        monitor: "video=${monitor.connector}:${toString monitor.width}x${toString monitor.height}@${toString monitor.refreshRate}"
      )
      monitors;

    loader = {
      grub = {
        enable = true;
        efiSupport = true;
        device = "nodev";
        useOSProber = true;
        theme = mkForce catppuccin;
        splashImage = mkForce "${catppuccin}/background.png";
        gfxmodeEfi = "${toString mainMonitor.width}x${toString mainMonitor.height}";
      };
      efi.canTouchEfiVariables = true;
    };
  };
}
